apply plugin: 'com.android.application'
// Crashlytics
apply plugin: 'io.fabric'
// Google Play Store Publish
apply plugin: 'com.github.triplet.play'

def getSigningKey(key) {
    def props = new Properties()
    file(signingConfigsFile).withInputStream { props.load(it) }
    return props.getProperty(key)
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "justtennis.com.justtennis"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 14
        versionName "2.2.9.14"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    // Google Play Store Publish
    signingConfigs {
        release {
            storeFile file(getSigningKey("storeFile"))
            storePassword getSigningKey("storePassword")
            keyAlias getSigningKey("keyAlias")
            keyPassword getSigningKey("keyPassword")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // Google Play Store Publish
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    dataBinding {
        enabled = true
    }

//    defaultConfig {
//        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
//    }


    // Google Play Store Publish
    play {
        // Can be one of the production, beta, alpha or rollout
        track = 'alpha'
        serviceAccountCredentials = file("store/Google Play Android Developer-97d8b56a531b.json")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    def versionSupport = "27.1.1"
    implementation "com.android.support:appcompat-v7:${versionSupport}"
    implementation "com.android.support:support-v4:${versionSupport}"
    implementation "com.android.support:recyclerview-v7:${versionSupport}"
    implementation "com.android.support:cardview-v7:${versionSupport}"
    implementation "com.android.support:design:${versionSupport}"

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.14'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // Firebase
    implementation 'com.google.firebase:firebase-core:16.0.4'
    // Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.5'

    // Network
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'

    // Picasso (Image loading)
    implementation 'com.squareup.picasso:picasso:2.71828'

    // Jackson (Json Parsing)
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.6'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.6'

    // Google Flex Layout
    implementation 'com.google.android:flexbox:0.2.3'
}

// Firebase
apply plugin: 'com.google.gms.google-services'